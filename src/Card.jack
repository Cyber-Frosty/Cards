class Card {
	field int value;
	field int suit;
	field boolean isFront;
	field boolean isOpened;
	
	field int currentColumn;
	field int currentRow;
	field int currentLayer;

	constructor Card new(int x, int y, boolean front, boolean opened) {
	    //col [0;6]
		//value: 1-13 (11=jack, 12=queen, 13=king)
		//suit: 0=diamond, 1=heart, 2=spade, 3=club
	    let value = x;
		let suit = y;
		let isFront = front;
		let isOpened = opened;
		return this;
	}
	method void dispose() {
		do Memory.deAlloc(this);
		return;
	}
	method void draw(int column, int row, int layer) {
	    if (isFront) {
		    if (isOpened) {
			    do Graphics.drawFullCard(column, row, layer, value, suit);
			}
			else {
			    do Graphics.drawClosedCardFull(column, row, layer);
			}
		}
	    else {
		    if (isOpened) {
			    do Graphics.drawCardPart(column, row, layer, value, suit);
			}
			else {
			    do Graphics.drawClosedCardPart(column, row, layer);
			}
		}
		
		let currentColumn = column;
		let currentRow = row;
		let currentLayer = layer;
		
		return;
	}
	method void erase() {
	    if (isFront) {
		    do Graphics.eraseFullCard(currentColumn, currentRow, currentLayer);
		}
		else {
		    do Graphics.eraseCardPart(currentColumn, currentRow, currentLayer);
		}
		return;
	}
	method void putFront() {
	    let isFront = true;
		return;
	}
	method void putBackAndErasePart() {
	    let isFront = false;
		do Graphics.eraseFullCardToPart(currentColumn, currentRow, currentLayer);
		return;
	}
	method void putBackAndEraseFull() {
	    let isFront = false;
		do Graphics.eraseFullCard(currentColumn, currentRow, currentLayer);
		return;
	}
	method int getSuit() {
		return suit;
	}
	method int getValue() {
		return value;
	}
	method boolean opened() {
		return isOpened;
	}
	method void open() {
		let isOpened = true;
		return;
	}
	method void close() {
		let isOpened = false;
		return;
	}
}