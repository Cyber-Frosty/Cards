class Pile {
    field Stack stack;
	field Stack toMove;
    field int column;
	field int row;

    constructor Pile new(int c, int r) {
        let stack = Stack.new();
		let toMove = Stack.new();
        let column = c;
		let row = r;
        return this;
    }

    method boolean canAddedToTableau(Card card) {
        var boolean isEmpty;
        var Card currentHead;
        var int currentSuit;
        var int cardSuit;
        var int currentValue;
        var int cardValue;
        var int checkValue;

        let isEmpty = stack.empty();
        if (isEmpty) {
            let cardValue = card.getValue();
            if (~(cardValue = 13)) {
                return false;
            }
            return true;
        }
        let currentHead = stack.getHead();
        let currentSuit = currentHead.getSuit();
        let cardSuit = card.getSuit();
        if (currentSuit < 2 & cardSuit < 2) {
            return false;
        }
        if (currentSuit > 1 & cardSuit > 1) {
            return false;
        }
        let currentValue = currentHead.getValue();
        let cardValue = card.getValue();
        let checkValue = currentValue - cardValue;
        if (~(checkValue = 1)) {
            return false;
        }
        return true;
    }
	
	method void drawSelection(int upperLayer, int lowerLayer) {
	    do Graphics.selectSubdeck(column, row, upperLayer * row, lowerLayer * row);
		return;
	}
	
	method void eraseSelection(int upperLayer, int lowerLayer) {
	    do Graphics.removeSubdeckSelection(column, row, upperLayer * row, lowerLayer * row);
		return;
	}

    method void addCardNewLayer(Card card) {
	    var int i;
	    var Card tmpCard;
		
		let tmpCard = stack.getHead();
		if (~(tmpCard = null)) {
			do tmpCard.putBackAndErasePart();
		}
		
        do stack.push(card);
		let i = stack.count() - 1;
		do card.draw(column, row, i);
        return;
    }
	
	method void addCardFirstLayer(Card card) {
	    var int i;
	    var Card tmpCard;
		
		let tmpCard = stack.getHead();
		if (~(tmpCard = null)) {
			do tmpCard.putBackAndEraseFull();
		}
		
        do stack.push(card);
		do card.draw(column, row, 0);
        return;
    }

    method Card removeCard() {
		var Card card;

		let card = stack.pop();
		do card.erase();
        return card;
    }

    method Card peekCard() {
        var Card card;

        let card = stack.getHead();
        return card;
    }

    method boolean checkHead() {
        var Card card;
        var boolean isOpened;

        let card = stack.getHead();
        if (card = null) {
            return false;
        }
        let isOpened = card.opened();
        if (isOpened) {
            return false;
        }
        do card.open();
        return true;
    }
	
	method void select() {
	    var int layer;
		var Card cardToMove;
		
		let layer = stack.count() - 1;
		if (~(layer < 0)) {
		    let cardToMove = stack.pop();
		    do toMove.push(cardToMove);
		    do drawSelection(layer, layer);
		}
	    
		return;
	}
	
	method void deselect() {
	    var int upperLayer;
		var int i;
		var Card cardToMove;
		
		let upperLayer = stack.count();
		let i = toMove.count();
		do eraseSelection(upperLayer, upperLayer + i - 1);
		while (i > 0) {
		    let cardToMove = toMove.pop();
			do stack.push(cardToMove);
		    let i = i - 1;
		}
		
		return;
	}
	
	method void increaseSubstack() {
	    var int stackCount;
		var int lowerLayer;
		var Card cardToMove;
		var Card moveHead;
		
		let stackCount = stack.count();
		if (stackCount > 0) {
		    let cardToMove = stack.getHead();
			if (~(cardToMove.opened())) {
			    return;
			}
			let moveHead = toMove.getHead();
			if (~(cardToMove.getValue() - moveHead.getValue() = 1)) {
			    return;
			}
			if ((cardToMove.getSuit() < 2) & (moveHead.getSuit() < 2)) {
			    return;
			}
			if ((cardToMove.getSuit() > 1) & (moveHead.getSuit() > 1)) {
			    return;
			}
			let lowerLayer = toMove.count() + stackCount - 1;
			do toMove.push(cardToMove);
			do stack.pop();
			do eraseSelection(stackCount, lowerLayer);
			do drawSelection(stackCount - 1, lowerLayer);
		}
		return;
	}
	
	method void decreaseSubstack() {
	    var int moveCount;
		var int stackCount;
		var int lowerLayer;
		var Card cardToReturn;
		
		let moveCount = toMove.count();
		if (moveCount > 1) {
		    let cardToReturn = toMove.getHead();
			let stackCount = stack.count();
			let lowerLayer = stackCount + moveCount - 1;
			do stack.push(cardToReturn);
			do toMove.pop();
			do eraseSelection(stackCount, lowerLayer);
			do drawSelection(stackCount + 1, lowerLayer);
		}
		return;
	}
	
	method int getSelectedCount() {
	    return toMove.count();
	}
	
	method Card getSubstackBottom() {
	    return toMove.getHead();
	}
	
	method Stack takeCardsToMove() {
		var int stackCount;
		var int moveCount;
		var int i;
		
		let moveCount = toMove.count();
		if (moveCount = 0) {
		    return toMove;
		}
		
		let stackCount = stack.count();
		do eraseSelection(stackCount, stackCount + moveCount - 1);		
		
	    return toMove;
	}
	
	method void redrawPileHead(boolean useLayers) {
	    var Card card;
		var int stackCount;
		
		let card = stack.getHead();
		if (~(card = null)) {
		    let stackCount = stack.count();
		    do card.open();
			do card.putFront();
			if (useLayers) {
			    do card.draw(column, row, stackCount - 1);
			}
			else {
			    do card.draw(column, row, 0);
			}
		}

		return;
	}

	method void redrawClosedStock() {
		var Card card;

		let card = stack.getHead();
		if (~(card = null)) {
			do card.putFront();
			do card.draw(column, row, 0);
		}

		return;
	}

	method boolean isEmpty() {
		return stack.empty();
	}

	method Stack getRefreshedStock() {
		var Stack tmpStack;
		var Card card;

		let tmpStack = Stack.new();
		let card = stack.pop();
		do card.erase();
		while (~(card = null)) {
			do card.close();
			do tmpStack.push(card);
			let card = stack.pop();
		}
	    
		return tmpStack;
	}

	method void refreshStock(Stack stock) {
		let stack = stock;
		return;
	}
}