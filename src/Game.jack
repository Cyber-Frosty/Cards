class Game {
    field int cursorColumn;
	field int cursorRow;
	field Pile pileTakeFrom;
	field boolean editingSubstack;
	
	field Array tableau;
	field Array stock;
	field Array foundations;
	
	constructor Game new() {
	    var Pile tmpPile;
	    var Card tmpCard;
		var int i;
		
	    let cursorColumn = 0;
		let cursorRow = 1;
		let editingSubstack = false;
		
	    let pileTakeFrom = null;
		
		let stock = Array.new(2);
		let foundations = Array.new(4);
		let i = 0;
		while (i < 4) {
		    let tmpPile = Pile.new(3 + i, 0, true, false, false, true, i);
		    let foundations[i] = tmpPile;
			let i = i + 1;
		}

	    let tableau = Array.new(7);
		let tmpPile = Pile.new(0, 1, true, false, false, false, 0);
		let tmpCard = Card.new(10, 0, true, true);
		do tmpPile.addCardNewLayer(tmpCard);
		let tmpCard = Card.new(1, 1, true, true);
		do tmpPile.addCardNewLayer(tmpCard);
		let tmpCard = Card.new(13, 3, true, true);
		do tmpPile.addCardNewLayer(tmpCard);
		let tmpCard = Card.new(12, 0, true, true);
		do tmpPile.addCardNewLayer(tmpCard);
		let tableau[0] = tmpPile;
		
		let tmpPile = Pile.new(1, 1, true, false, false, false, 0);
		let tableau[1] = tmpPile;
		
		do Graphics.addCursor(cursorColumn, cursorRow, 0);
		
		return this;
	}
	
	method void selectSubstack() {
	    if (pileTakeFrom = null) {
		    if ((cursorRow = 0) & (cursorColumn = 0)) {
				return;
			}
			let pileTakeFrom = getCurrentPile();
		}
		
		do Graphics.removeCursor(cursorColumn, cursorRow, 0);
		do pileTakeFrom.select();
		let editingSubstack = true;
		
		return;
	}
	
	method Pile getCurrentPile() {
	    if (cursorRow = 1) {
			return tableau[cursorColumn];
		} else {
			if (cursorColumn < 2) {
				return stock[cursorColumn];
			}
            if (cursorColumn > 2) {
				return foundations[cursorColumn - 3];
			}
		}
		return null;
	}
	
	method void increaseSubstack() {
	    if (~(pileTakeFrom = null)) {
		    do pileTakeFrom.increaseSubstack();
		}
		
	    return;
	}
	
	method void decreaseSubstack() {
	    if (~(pileTakeFrom = null)) {
		    do pileTakeFrom.decreaseSubstack();
		}
		
	    return;
	}
	
	method void goRight() {
	    var boolean erase;
		var Pile pileOn;
		
	    if (cursorColumn = 6) {
		    return;
		}
		
		if (editingSubstack) {
		    let editingSubstack = false;
		} else {
		    do Graphics.removeCursor(cursorColumn, cursorRow, 0);
		}
		
		let cursorColumn = cursorColumn + 1;
		if ((cursorRow = 0) & (cursorColumn = 2)) {
		    let cursorColumn = 3;
		}
		
		if (~(pileTakeFrom = null)) {
		    let pileOn = getCurrentPile();
			if (pileOn = pileTakeFrom) {
			    let editingSubstack = true;
			    return;
			}
		}
		do Graphics.addCursor(cursorColumn, cursorRow, 0);
	    return;
	}
	
	method void goLeft() {
	    var boolean erase;
		var Pile pileOn;
	
	    if (cursorColumn = 0) {
		    return;
		}
		
		if (editingSubstack) {
		    let editingSubstack = false;
		} else {
		    do Graphics.removeCursor(cursorColumn, cursorRow, 0);
		}
		
		let cursorColumn = cursorColumn - 1;
		if ((cursorRow = 0) & (cursorColumn = 2)) {
		    let cursorColumn = 1;
		}
		
		if (~(pileTakeFrom = null)) {
		    let pileOn = getCurrentPile();
			if (pileOn = pileTakeFrom) {
			    let editingSubstack = true;
			    return;
			}
		}
		do Graphics.addCursor(cursorColumn, cursorRow, 0);
	    return;
	}
	
	method void moveSubstack() {
		var int moveCount;
		
	    if (pileTakeFrom = null) {
		    return;
		}
		
		if (cursorRow = 1) {
		    do moveToTableau();
		} 
		else {
		    if (cursorColumn < 3) {
			    return;
			}
			do moveToFoundation();
		}
		
		return;
	}
	
	method void moveToTableau() {
	    var Pile pileTo;
	    var Card bottomCard;
		var Stack toMove;
		var int moveCount;
		
	    let pileTo = tableau[cursorColumn];
		if (pileTo = pileTakeFrom) {
			return;
		}
		
		let bottomCard = pileTakeFrom.getSubstackBottom();
		if (~(pileTo.canAdded(bottomCard))) {
		    return;
		}
	    
		let toMove = pileTakeFrom.takeCardsToMove();
		let moveCount = toMove.count();
		while (moveCount > 0) {
		    let bottomCard = toMove.getHead();
			do pileTo.addCardNewLayer(bottomCard);
			do toMove.pop();
			let moveCount = moveCount - 1;
		}
		
		let pileTakeFrom = null;
		do Graphics.addCursor(cursorColumn, cursorRow, 0);
		return;
	}
	
	method void moveToFoundation() {
	    var Pile pileTo;
	    var Card movedCard;
		var Card pileCard;
		var Stack toMove;
		var int moveCount;
		var int suit;
		
		let moveCount = pileTakeFrom.getSelectedCount();
		
		if (moveCount > 1) {
		    return;
		}
		
		let movedCard = pileTakeFrom.getSubstackBottom();
		let pileTo = foundations[movedCard.getSuit()];
		let pileCard = pileTo.peekCard();
		
		if (pileCard = null) {
		    if (movedCard.getValue() > 1) {
			    return;
			}
		}
		else {
		    if (~(movedCard.getValue() - pileCard.getValue() = 1)) {
			    return;
			}
		}
		
		let toMove = pileTakeFrom.takeCardsToMove();
		do toMove.pop();
	    do pileTo.addCardFirstLayer(movedCard);
		
		let pileTakeFrom = null;
		do Graphics.addCursor(cursorColumn, cursorRow, 0);
		do toMove.dispose();
		return;
	}
}